OUTPUT_ARCH(mips)
ENTRY(_start)
SECTIONS {
	. = 0x80001000;          /* Image starts here                     */
	.text   :
	{
		*(.text .text.*)     /* asm text, then C text                 */
		*(.rodata .rodata.*) /* asm and C read-only data              */
		_etext = . ;         /* provide _etext constant               */
	}

	.data   :
	{
		*(.data .data.*)     /* asm and C data                        */
		_edata = . ;         /* end of data constant                  */
	}

	.bss    :
	{
		_bss = . ;           /* beginning of bss segment              */
		*(.bss .bss.*)       /* asm and C bss                         */
		*(COMMON)            /* extra sections that are common        */
		_end = . ;           /* end of image constant                 */
	}

	/* Discard comment and note (but not debugging) sections.  Some
	* versions of GNU ld would otherwise automatically place the
	* ".note.gnu.build-id" section before _start!  */
	/DISCARD/ : {
		*(.comment .comment.* .note .note.*)
	}
}
