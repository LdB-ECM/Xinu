/**
 * @file ld.script
 *
 * This is the linker script for the Xinu kernel on the Raspberry Pi.  The GPU
 * loads the kernel at address 0x8000 by default, and the ARM processor starts
 * executing instructions at that address.  We place _start, the entry point for
 * Xinu, in a special .init section to ensure it is placed first in the kernel
 * image.
 *
 * Because of this direct jump to address 0x8000, the "ENTRY(_start)" line below
 * does nothing to set the real entry point of the kernel; however, it _is_
 * needed to specify the entry point to the linker when it is executed with the
 * --gc-sections option, which removes all sections not reachable from the
 * program's entry point.
 *
 * To be safe, we ensure that every section is aligned on a 64-byte (cache line)
 * boundary.
 */
/* Embedded Xinu, Copyright (C) 2013.  All rights reserved. */

ENTRY(_start)

SECTIONS {
    . = 0x8000;
    .init : {
		. = ALIGN(4);
        *(.init .init.*)
    }

    . = ALIGN(64);
    
    .text : {
		. = ALIGN(4);
        *(.text .text.*)
        *(.rodata .rodata.*)
        _etext = .;
    }

    . = ALIGN(64);

    .data : {
		. = ALIGN(4);
        *(.data .data.*)
    }

    . = ALIGN(64);

    _bss = . ;
    .bss : {
		. = ALIGN(4);
		__bss_start__ = .;     	/* Label in case we want address of BSS section start */
        *(.bss .bss.*)
		__bss_end__ = .;     	/* Label in case we want address of BSS section end */
    } 

    . = ALIGN(64);
    _end = .;

    /* Discard comment and note (but not debugging) sections.  Some
     * versions of GNU ld would otherwise automatically place the
     * ".note.gnu.build-id" section before _start!  */
    /DISCARD/ : {
        *(.comment .comment.* .note .note.*)
    }
}
